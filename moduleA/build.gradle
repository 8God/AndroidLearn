if (rootProject.ext.isBuildMudle.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
apply from: "../config.gradle"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    sourceSets {
        main {
            if (rootProject.ext.isBuildMudle.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //排除java/debug文件夹下的所有文件,一般会存放testactivity，防止打包进去
                    exclude 'debug/**'
                }
            }
        }
    }
    resourcePrefix "modulea_"
}
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    compile project(':iocApi')
    annotationProcessor project(':iocCompiler')

    compile('com.alibaba:arouter-api:1.2.1.1')
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.2.1'
    compile project(':moduleBase')
}
